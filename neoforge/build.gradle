plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev'
}

dependencies {
    implementation "maven.modrinth:en-sityfuntion:0.1.2+1.21-neoforge"
    implementation "maven.modrinth:sodium:mc1.21.5-0.6.13-neoforge"
    implementation "maven.modrinth:hide-experimental-warning:1.21.5-1.2-fabric+forge+neo"
    implementation "maven.modrinth:collective:1.21.5-8.3-fabric+forge+neo"
}

neoForge {
    version = neoforge_version
    // Automatically enable neoforge AccessTransformers if the file exists
    def at = project(':common').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.add(at.absolutePath)
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
    runs {
        configureEach {
            systemProperty('neoforge.enabledGameTestNamespaces', mod_id)
            ideName = "NeoForge ${it.name.capitalize()} (${project.path})" // Unify the run config names with fabric
        }
        client {
            client()
        }
        data {
            data()
        }
        server {
            server()
        }
    }
    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }